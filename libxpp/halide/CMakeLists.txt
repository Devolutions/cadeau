
set(HALIDE_LLVM_BIN_DIR "${HALIDE_LLVM_PREFIX}/bin")
set(HALIDE_LLVM_COMPILER "${HALIDE_LLVM_BIN_DIR}/clang++")

if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(HALIDE_GENERATOR_CXXFLAGS "-Xclang;-flto-visibility-public-std;-Wno-inconsistent-dllimport")
else()
	set(HALIDE_GENERATOR_CXXFLAGS "-std=c++11;-fno-rtti")
	if(CMAKE_HOST_SYSTEM_NAME STREQUAL Darwin)
		list(APPEND HALIDE_GENERATOR_CXXFLAGS "-stdlib=libc++")
	else()
		# https://gcc.gnu.org/onlinedocs/libstdc++/manual/using_dual_abi.html
		list(APPEND HALIDE_GENERATOR_CXXFLAGS "-D_GLIBCXX_USE_CXX11_ABI=0")
	endif()
endif()

# clang targets:
#
# i686-pc-windows-msvc
# i686-apple-darwin
# i686-unknown-linux-gnu
#
# x86_64-pc-windows-msvc
# x86_64-apple-darwin
# x86_64-unknown-linux-gnu

# https://github.com/wayk/Halide/blob/master/src/Target.cpp#L180
# https://github.com/halide/Halide/blob/master/tutorial/lesson_15_generators_usage.sh

if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(HALIDE_GENERATOR_LDFLAGS "-lHalide")
	set(HALIDE_GENERATOR_TARGET "i686-pc-windows-msvc")
	set(HALIDE_SHARED_LIBRARY "Halide.dll")
	set(HALIDE_OBJECT_SUFFIX ".obj")
	set(HALIDE_HOST "windows")
else()
	set(HALIDE_GENERATOR_LDFLAGS "-lHalide")
	list(APPEND HALIDE_GENERATOR_LDFLAGS "-lpthread;-ldl")
	list(APPEND HALIDE_GENERATOR_LDFLAGS "-rpath;${HALIDE_LIBRARY_DIR}")
	if(CMAKE_HOST_SYSTEM_NAME STREQUAL Darwin)
		set(HALIDE_GENERATOR_TARGET "x86_64-apple-darwin")
		set(HALIDE_SHARED_LIBRARY "libHalide.dylib")
		set(HALIDE_HOST "osx")
		list(APPEND HALIDE_GENERATOR_LDFLAGS "-mmacosx-version-min=10.9")
	else()
		if(WAYK_HOST_ARCH STREQUAL amd64)
			set(HALIDE_GENERATOR_TARGET "x86_64-unknown-linux-gnu")
		else()
			set(HALIDE_GENERATOR_TARGET "i686-unknown-linux-gnu")
		endif()
		set(HALIDE_SHARED_LIBRARY "libHalide.so")
		set(HALIDE_HOST "linux")
	endif()
	set(HALIDE_OBJECT_SUFFIX ".o")
endif()

if(IOS)
	set(HALIDE_TARGET "ios")
	set(HALIDE_OBJECT_SUFFIX ".o")
elseif(ANDROID)
	set(HALIDE_TARGET "android")
	set(HALIDE_OBJECT_SUFFIX ".o")
else()
	set(HALIDE_TARGET "${HALIDE_HOST}")
endif()

list(APPEND HALIDE_GENERATOR_LDFLAGS "--target=${HALIDE_GENERATOR_TARGET}")

set(HALIDE_GENERATOR_NAME "xpp-halide-generator")
if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(HALIDE_GENERATOR_NAME "${HALIDE_GENERATOR_NAME}.exe")
endif()
set(HALIDE_GENERATOR_SOURCES "generator.cpp")
set(HALIDE_GENERATOR_EXECUTABLE "${CMAKE_CURRENT_BINARY_DIR}/${HALIDE_GENERATOR_NAME}")
set(HALIDE_OUTPUT_PATH "${CMAKE_BINARY_DIR}/generated/halide")

if(CMAKE_HOST_SYSTEM_NAME STREQUAL Windows)
	set(HALIDE_CUSTOM_COMMAND "${CMAKE_COMMAND};-E;env;--unset=INCLUDE;--unset=LIB;--unset=LIBPATH;--unset=PATH")
elseif(CMAKE_HOST_SYSTEM_NAME STREQUAL Darwin)
	set(HALIDE_CUSTOM_COMMAND "${CMAKE_COMMAND};-E;env;--unset=SDKROOT;--unset=IPHONEOS_DEPLOYMENT_TARGET")
endif()

#list(APPEND HALIDE_CUSTOM_COMMAND "${CMAKE_COMMAND};-E;environment")
list(APPEND HALIDE_CUSTOM_COMMAND "${HALIDE_LLVM_COMPILER}"
		"${CMAKE_CURRENT_SOURCE_DIR}/${HALIDE_GENERATOR_SOURCES}"
		${HALIDE_GENERATOR_CXXFLAGS} -I ${HALIDE_INCLUDE_DIR} -L ${HALIDE_LIBRARY_DIR}
		${HALIDE_GENERATOR_LDFLAGS} -o ${HALIDE_GENERATOR_NAME} -v)

add_custom_command(
	COMMAND ${CMAKE_COMMAND} -E make_directory "${HALIDE_OUTPUT_PATH}"
	COMMAND ${CMAKE_COMMAND} -E copy_if_different "${HALIDE_LIBRARY_DIR}/${HALIDE_SHARED_LIBRARY}" ${HALIDE_SHARED_LIBRARY}
	COMMAND ${HALIDE_CUSTOM_COMMAND}
	OUTPUT ${HALIDE_GENERATOR_EXECUTABLE}
	DEPENDS ${HALIDE_GENERATOR_SOURCES})

if(CMAKE_OSX_ARCHITECTURES)
	foreach(CMAKE_OSX_ARCHITECTURE ${CMAKE_OSX_ARCHITECTURES})
		if(CMAKE_OSX_ARCHITECTURE STREQUAL "i386")
			list(APPEND HALIDE_ARCHS "x86-32")
		elseif(CMAKE_OSX_ARCHITECTURE STREQUAL "x86_64")
			list(APPEND HALIDE_ARCHS "x86-64")
		elseif(CMAKE_OSX_ARCHITECTURE STREQUAL "armv7")
			list(APPEND HALIDE_ARCHS "arm-32")
		elseif(CMAKE_OSX_ARCHITECTURE STREQUAL "armv7s")
			list(APPEND HALIDE_ARCHS "arm-32-armv7s")
		elseif(CMAKE_OSX_ARCHITECTURE STREQUAL "arm64")
			list(APPEND HALIDE_ARCHS "arm-64")
		endif()
	endforeach()
elseif(ANDROID)
	if(ANDROID_SYSROOT_ABI STREQUAL "x86")
		list(APPEND HALIDE_ARCHS "x86-32")
	elseif(ANDROID_SYSROOT_ABI STREQUAL "x86_64")
		list(APPEND HALIDE_ARCHS "x86-64")
	elseif(ANDROID_SYSROOT_ABI STREQUAL "arm")
		list(APPEND HALIDE_ARCHS "arm-32")
	elseif(ANDROID_SYSROOT_ABI STREQUAL "arm64")
		list(APPEND HALIDE_ARCHS "arm-64")
	endif()
else()
	if(CMAKE_SIZEOF_VOID_P EQUAL 8)
		list(APPEND HALIDE_ARCHS "x86-64")
	else()
		list(APPEND HALIDE_ARCHS "x86-32")
	endif()
endif()

set(HALIDE_GENERATORS "YCoCgR420ToRgb;RgbToYCoCgR420;Compare8Stage1;Compare32Stage1;Copy")

# possibly wrong: generate headers for a given target, and use them for all targets.
# this works as long as there is no real difference between the headers, which seems to be the case.
foreach(HALIDE_GENERATOR ${HALIDE_GENERATORS})
	add_custom_command(COMMAND ${HALIDE_GENERATOR_EXECUTABLE} ARGS
		-g ${HALIDE_GENERATOR} -e h -o "${HALIDE_OUTPUT_PATH}" target=${HALIDE_TARGET}-x86-64-no_runtime
		OUTPUT ${HALIDE_OUTPUT_PATH}/${HALIDE_GENERATOR}.h
		DEPENDS ${HALIDE_GENERATOR_EXECUTABLE})

	list(APPEND HALIDE_GENERATED_HEADERS "${HALIDE_OUTPUT_PATH}/${HALIDE_GENERATOR}.h")
endforeach()

foreach(HALIDE_ARCH ${HALIDE_ARCHS})
	foreach(HALIDE_GENERATOR ${HALIDE_GENERATORS})
		set(BASE_NAME "${HALIDE_GENERATOR}_${HALIDE_ARCH}")

		add_custom_command(COMMAND ${HALIDE_GENERATOR_EXECUTABLE} ARGS
			-g ${HALIDE_GENERATOR} -e h -o "${HALIDE_OUTPUT_PATH}" -n ${BASE_NAME}
			target=${HALIDE_TARGET}-${HALIDE_ARCH}-no_runtime
			OUTPUT ${HALIDE_OUTPUT_PATH}/${BASE_NAME}.h
			DEPENDS ${HALIDE_GENERATOR_EXECUTABLE})

		list(APPEND HALIDE_GENERATED_HEADERS "${HALIDE_OUTPUT_PATH}/${BASE_NAME}.h")

		add_custom_command(COMMAND ${HALIDE_GENERATOR_EXECUTABLE} ARGS
			-g ${HALIDE_GENERATOR} -e o -o "${HALIDE_OUTPUT_PATH}" -n ${BASE_NAME}
			target=${HALIDE_TARGET}-${HALIDE_ARCH}-no_runtime
			OUTPUT ${HALIDE_OUTPUT_PATH}/${BASE_NAME}${HALIDE_OBJECT_SUFFIX}
			DEPENDS ${HALIDE_GENERATOR_EXECUTABLE})

		list(APPEND HALIDE_GENERATED_OBJECTS "${HALIDE_OUTPUT_PATH}/${BASE_NAME}${HALIDE_OBJECT_SUFFIX}")
	endforeach()

	add_custom_command(COMMAND ${HALIDE_GENERATOR_EXECUTABLE} ARGS
		-r "halide_runtime_${HALIDE_ARCH}" -e o -o "${HALIDE_OUTPUT_PATH}" target=${HALIDE_TARGET}-${HALIDE_ARCH}
		OUTPUT ${HALIDE_OUTPUT_PATH}/halide_runtime_${HALIDE_ARCH}${HALIDE_OBJECT_SUFFIX}
		DEPENDS ${HALIDE_GENERATOR_EXECUTABLE})

	list(APPEND HALIDE_GENERATED_OBJECTS "${HALIDE_OUTPUT_PATH}/halide_runtime_${HALIDE_ARCH}${HALIDE_OBJECT_SUFFIX}")
endforeach()

list(REMOVE_DUPLICATES HALIDE_GENERATED_HEADERS)

add_custom_target(halide-generate-all ALL DEPENDS ${HALIDE_GENERATOR_EXECUTABLE} ${HALIDE_GENERATED_HEADERS} ${HALIDE_GENERATED_OBJECTS})

set(HALIDE_GENERATED_HEADERS ${HALIDE_GENERATED_HEADERS} PARENT_SCOPE)
set(HALIDE_GENERATED_OBJECTS ${HALIDE_GENERATED_OBJECTS} PARENT_SCOPE)
#set(HALIDE_OUTPUT_PATH ${HALIDE_OUTPUT_PATH} PARENT_SCOPE)
